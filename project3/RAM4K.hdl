// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/b/RAM4K.hdl
/**
 * Memory of 4K 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM4K {
    IN in[16], load, address[12];
    OUT out[16];

    // We have 4096 registers, so we need log4096 = 12 address bits
    // However, we are building it using 8 RAM512 parts
    // So use first 3 address bits to select the RAM512 part
    // Send the next 9 address bits to the selected RAM512 part 
    // The RAM512 part uses first 3 bits to select one of it's 8 RAM64 parts
    // The RAM64 part uses the next 3 bits to select one of it's 8 RAM8 parts
    // The RAM8 part uses the last 3 bits to select one of it's 8 registers
    
    PARTS:
    // Fan out the load to the 8 RAM512 parts based on first 3 bits of address selector
    // First 3 bits to determine which RAM512 part
    DMux8Way(in=load , sel=address[9..11] , a=a , b=b , c=c , d=d , e=e , f=f , g=g , h=h);

    // Next 9 bits to select the RAM512 part
    RAM512(in=in , load=a , address=address[0..8] , out=outa );
    RAM512(in=in , load=b , address=address[0..8] , out=outb );
    RAM512(in=in , load=c , address=address[0..8] , out=outc );
    RAM512(in=in , load=d , address=address[0..8] , out=outd );
    RAM512(in=in , load=e , address=address[0..8] , out=oute );
    RAM512(in=in , load=f , address=address[0..8] , out=outf );
    RAM512(in=in , load=g , address=address[0..8] , out=outg );
    RAM512(in=in , load=h , address=address[0..8] , out=outh );

    // Now, select the RAM512 part which has the output based on first 3 bits of address
    Mux8Way16(a=outa , b=outb , c=outc , d=outd , e=oute , f=outf , g=outg, h=outh , sel=address[9..11] , out=out);
}